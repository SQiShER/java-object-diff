buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'ru.vyarus:gradle-animalsniffer-plugin:1.4.4'
		classpath "com.smokejumperit.gradle.license:Gradle-License-Report:0.0.2"
	}
}

plugins {
	id 'java'
	id 'groovy'
	id 'osgi'
	id 'net.saliman.cobertura' version '2.6.0'
	id 'com.github.kt3k.coveralls' version '2.8.1'
	id 'com.github.ben-manes.versions' version '0.14.0'
	id 'com.jfrog.bintray' version '1.7.3'
	id 'net.researchgate.release' version '2.5.0'
	id 'com.github.jk1.dependency-license-report' version '0.3.8'
}

apply plugin: 'ru.vyarus.animalsniffer'
apply plugin: 'maven-publish'

group = 'de.danielbechler'

description = """Java Object Diff"""

sourceCompatibility = 1.5
targetCompatibility = 1.5

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

repositories {
	mavenCentral()
}

sourceSets {
	main {
		java {
			srcDirs = ['src/main/java']
		}
		groovy {
			srcDirs = []
		}
	}
	test {
		java {
			srcDirs = []
		}
		groovy {
			srcDirs = ['src/test/java']
		}
	}
	intTest {
		groovy {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDirs = ['src/integration-test/java']
		}
	}
}

configurations {
	intTestCompile.extendsFrom testCompile
	intTestRuntime.extendsFrom testRuntime
}

task intTest(type: Test) {
	testClassesDirs = sourceSets.intTest.output.classesDirs
	classpath = sourceSets.intTest.runtimeClasspath
}

check.dependsOn intTest

compileGroovy {
	// somehow the groovy compile deletes the java compiled classes from the build directory
	dependsOn = []
}

jar {
	manifest {
		instruction 'Bundle-Vendor', 'Daniel Bechler'
		instruction 'Bundle-DocURL', 'https://github.com/SQiShER/java-object-diff'
		instruction 'Export-Package', '{local-packages}'
	}
}

dependencies {
	signature 'org.codehaus.mojo.signature:java15:1.0@signature'
	signature 'org.codehaus.mojo.signature:java16:1.1@signature'
	signature 'org.codehaus.mojo.signature:java17:1.0@signature'
	// For Android support:
	// signature 'net.sf.androidscents.signature:android-api-level-23:6.0_r3@signature'
	compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.22'
	testCompile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.8'
	testCompile group: 'ch.qos.logback', name: 'logback-core', version: '1.2.1'
	testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.1'
	testCompile group: 'org.spockframework', name: 'spock-core', version: '1.0-groovy-2.4'
	testCompile group: 'cglib', name: 'cglib-nodep', version: '3.2.4'
	testCompile group: 'org.objenesis', name: 'objenesis', version: '2.5.1'
}

cobertura {
	coverageFormats = ['html', 'xml']
	coverageIgnoreTrivial = true
}

bintray {
	user = System.getenv('BINTRAY_USER')
	key = System.getenv('BINTRAY_KEY')
	publications = ['mavenJava']
	publish = true
	pkg {
		repo = 'maven'
		name = 'java-object-diff'
		licenses = ['Apache-2.0']
		vcsUrl = 'https://github.com/SQiShER/java-object-diff.git'
		githubRepo = 'SQiShER/java-object-diff'
		githubReleaseNotesFile = 'README.md'
		version {
			name = project.version
			released = new Date()
			vcsTag = rootProject.name + '-' + project.version
			gpg {
				sign = true
				passphrase = System.getenv('BINTRAY_GPG_PASSPHRASE')
			}
			mavenCentralSync {
				user = System.getenv('MAVEN_CENTRAL_USER_TOKEN_KEY')
				password = System.getenv('MAVEN_CENTRAL_USER_TOKEN_PASSWORD')
			}
		}
	}
}

javadoc {
	failOnError = false
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar, javadocJar
}

def pomConfig = {
	licenses {
		license {
			name "The Apache Software License, Version 2.0"
			url "http://www.apache.org/licenses/LICENSE-2.0.txt"
			distribution "repo"
		}
	}
	developers {
		developer {
			id "sqisher"
			name "Daniel Bechler"
			url "https://github.com/SQiShER"
		}
	}
	scm {
		url "https://github.com/SQiShER/java-object-diff"
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			pom.withXml {
				def root = asNode()
				root.appendNode('description', 'Library to diff and merge Java objects with ease')
				root.appendNode('name', 'java-object-diff')
				root.appendNode('url', 'https://github.com/SQiShER/java-object-diff')
				root.children().last() + pomConfig
			}
		}
	}
}

release {
	tagTemplate = '$name-$version'
}

@SuppressWarnings("GroovyAssignabilityCheck")
static processFileInplace(File file, Closure processText) {
	String text = file.text
	file.write(processText(text))
}

task updateDocs {
	doLast {
		def updateVersion = { String text ->
			text = text.replaceAll('<version>[^<]+</version>', "<version>${version}</version>")
			text = text.replaceAll('de\\.danielbechler:java-object-diff:[0-9-A-Z\\-.]+', "de.danielbechler:java-object-diff:${version}")
			return text
		}
		processFileInplace(file('README.md'), updateVersion)
		processFileInplace(file('docs/maven.md'), updateVersion)
	}
}

afterReleaseBuild.dependsOn bintrayUpload
afterReleaseBuild.dependsOn updateDocs